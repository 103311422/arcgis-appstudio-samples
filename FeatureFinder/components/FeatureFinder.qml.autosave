import QtQuick 2.5
import QtQuick.Controls 1.2
import QtQuick.Layouts 1.1
import QtQuick.Controls.Styles 1.3

import ArcGIS.AppFramework 1.0

Item {
    id: _root

    // Configurable variables
    property color searchBoxColor: "white"
    property color searchBoxBorderColor: "gray"
    property string fontFamilyName: ""
    property color searchBoxTextColor: "#585858"
    property double fontSize: 16*scaleFactor
    property color searchBoxPlaceholderTextColor: "#A3A199"
    property string searchBoxPlaceHolderText
    property color busyIndicatorColor: "blue"
    property double dropDownListMaxHeight: 175*scaleFactor
    property int charMin: 3

    // Signals
    signal textEntered(string text)
    signal itemClicked(string text)
    signal returnPressed(string text)
    signal clearText()

    // Other variables
    property var valuesList: []
    property bool valueSelected: false
    property double originalSearchBoxHeight: _root.height
    property int currentIndex: 1
    // Scale factor
    property double scaleFactor : AppFramework.displayScaleFactor


    // -- Search rectangle expands to show filtered list of values
    Rectangle {
        id: rectSearch
        width: parent.width
        height: parent.height

        color: searchBoxColor
        border.color: searchBoxBorderColor
        border.width: 1*scaleFactor

        Rectangle {
            id: rectBorder
            width: parent.width
            height: 1*scaleFactor
            color: searchBoxBorderColor
            anchors {
                top: txtSearch.bottom
            }
            visible: false
        }

        TextField {
            id: txtSearch
            style: TextFieldStyle {
                placeholderTextColor: searchBoxPlaceholderTextColor
                background: Rectangle {
                    height: parent.height
                    width: parent.width - rectClearText.width
                    color: searchBoxColor
                }
            }
            height: originalSearchBoxHeight - 4*scaleFactor
            width: parent.width - rectClearText.width
            font.family: fontFamilyName
            font.pointSize: fontSize
            textColor: searchBoxTextColor
            activeFocusOnPress: true
            placeholderText: searchBoxPlaceHolderText
            anchors {
                left: parent.left
                top: parent.top
                margins: 2*scaleFactor
            }

            Keys.onReturnPressed: {
                // Select first item in list
                if (valuesList.length > 0 && valuesList[0] !== " ") {
                    returnPressed(valuesList[0])
                    //                }else if (text.length > 0) {
                    //                    returnPressed(text)
                    //                }
                }
            }

            onFocusChanged: {
                if (focus) {
                    placeholderText = ""
                }else {
                    placeholderText = searchBoxPlaceHolderText
                }
            }

            onTextChanged: {
                var whereList = []
                var where = ""
                if (cursorPosition >= charMin && !valueSelected) {
                    textEntered(text)
                    // Insert empty in list for busy indicator
                    if (valuesList[0] !== " ") {
                        valuesList.splice(0, 0, " ")
                    }
                    flickableValuesList.contentY = 0
                    repeaterValuesList.model = valuesList
                    updateRectHeight()
                    // Turn on busy indicator
                    rectSwatches.visible = true
                    timer.running = true

                }else {
                    valuesList = []
                    repeaterValuesList.model = valuesList
                    updateRectHeight()
                }
            }
        }


        // -- Clear text from search box and show only placeholder text
        Rectangle {
            id: rectClearText
            height: Math.min(originalSearchBoxHeight - 4*scaleFactor, 25*scaleFactor)
            width: height
            color: searchBoxColor
            anchors {
                verticalCenter: txtSearch.verticalCenter
                right: parent.right
                rightMargin: 4*scaleFactor
            }

            Image {
                id: imgClearText
                source: "images/clear_text.png"
                anchors.fill: parent
                visible: txtSearch.text !== "" || txtSearch.focus === true
            }

            MouseArea {
                anchors.fill: parent
                onClicked: {
                    mouse.accepted = false
                    if (imgClearText.visible) {
                        txtSearch.text = ""
                        txtSearch.focus = false
                        clearText()
                    }
                }
            }
        }


        // -- Scrollable filtered list of values that match the user's input
        Flickable {
            id: flickableValuesList
            width: parent.width - (10*scaleFactor)
            height: rectSearch.height - (40*scaleFactor)
            contentHeight: (repeaterValuesList.count*20*scaleFactor) + (repeaterValuesList.count*5*scaleFactor)
            clip: true
            anchors {
                top: txtSearch.bottom
                topMargin: 2*scaleFactor
                left: parent.left
                leftMargin: 8*scaleFactor
            }

            Column {
                spacing: 5*scaleFactor

                Repeater {
                    id: repeaterValuesList
                    model: valuesList

                    Rectangle {
                        width: flickableValuesList.width - (8*scaleFactor)
                        height: 20*scaleFactor
                        color: "transparent"

                        Text {
                            id: txtValue
                            text: modelData
                            font.pointSize: fontSize
                            font.family: fontFamilyName
                            color: searchBoxTextColor
                        }

                        MouseArea {
                            anchors.fill: parent
                            onClicked: {
                                // Zoom to, and select the feature
                                if (txtValue !== " ") {
                                    itemClicked(modelData)
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    // -- Busy Indicator --
    Rectangle {
        id: rectSwatches
        width: parent.width
        height: 10*scaleFactor
        color: "transparent"
        visible: false
        anchors {
            bottom: parent.bottom
            bottomMargin: -20*scaleFactor
        }
        Row {
            spacing: 5*scaleFactor
            anchors {
                left: parent.left
                leftMargin: 10*scaleFactor
            }

            Repeater {
                model: [1,2,3]
                id: listSwatches
                Rectangle {
                    color: busyIndicatorColor
                    opacity: currentIndex >= index + 1 ? 1 : 0.3
                    width: 9*scaleFactor
                    height: 9*scaleFactor
                    radius: 5*scaleFactor
                }
            }
        }
    }

    Timer {
        id: timer
        interval: 425
        running: false
        repeat: true

        onTriggered:{
            currentIndex = currentIndex + 1
            if (currentIndex > 3) {
                currentIndex = 0
            }
        }
    }

    // -- Update height of drop-down list of values
    function updateRectHeight() {
        if (valuesList.length > 0) {
            if (valuesList.length > 1) {
                rectSearch.height = Math.min(dropDownListMaxHeight, txtSearch.height + (28*scaleFactor*valuesList.length))
            }else {
                // Little extra padding is needed when only one item in drop-down list
                rectSearch.height = Math.min(dropDownListMaxHeight, txtSearch.height + (30*scaleFactor*valuesList.length))
            }
            rectBorder.visible = true

        } else {
            rectSearch.height = originalSearchBoxHeight
            rectBorder.visible = false
        }
    }


    // -- Find feature based on user's input value
    function resetSearchBox(selectedValue) {
        // Update value selected bool to true to make drop-down list of values disappear
        valueSelected = true

        // Update text field to show selected value
        txtSearch.text = selectedValue

        // Clear valuesList to make drop-down list disappear
        valuesList = []

        // Call updateRectHeight function to return the drop down rectangle to its original size
        updateRectHeight()

        // Refresh Repeater model
        repeaterValuesList.model = valuesList

        // Re-set value selected bool - this enables the drop-down list of values to appear
        // when the user continues typing in the text field
        valueSelected = false

        // Set text box focus to false
        txtSearch.focus = false
    }

    // -- Update filtered list of values displayed in drop-down based on user input text
    function updateValuesList(filterValuesList) {
        valuesList = []

        valuesList.push(" ")

        for (var i = 0; i < filterValuesList.length; i++) {
            valuesList.push(filterValuesList[i])
        }

        // Sort list
        valuesList.sort()

        // Turn off busy indicator
        valuesList.splice(0, 1)
        rectSwatches.visible = false
        timer.running = false

        // Update model
        repeaterValuesList.model = valuesList
        // Update height of search rectangle
        updateRectHeight()

        // Re-set current index for busy indicator
        currentIndex = 1
    }

}

